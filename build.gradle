plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.4.4"
    id("io.micronaut.aot") version "4.4.4"
    id 'com.google.protobuf' version '0.9.5'
}

version = "0.1.3"
group = "datasahi.flow"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    annotationProcessor("io.micronaut:micronaut-http-validation")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    compileOnly("io.micronaut:micronaut-http-client")

    implementation 'com.moilioncircle:redis-replicator:3.9.0'
    implementation 'org.json:json:20250107'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation('org.apache.commons:commons-jexl3:3.4.0')
    implementation("org.apache.commons:commons-lang3:3.12.0")

    implementation("org.sql2o:sql2o:1.6.0")
    implementation("com.zaxxer:HikariCP:5.0.1")

    // internal database
    implementation("org.mapdb:mapdb:3.1.0")
    implementation("org.rocksdb:rocksdbjni:9.10.0")

    // all database drivers
    implementation("redis.clients:jedis:5.2.0")
    implementation("com.mysql:mysql-connector-j:9.2.0")
    implementation("com.microsoft.sqlserver:mssql-jdbc:12.8.1.jre11")
    implementation("org.apache.activemq:activemq-client:5.18.3")
    implementation("com.hierynomus:smbj:0.14.0")

    implementation("io.grpc:grpc-netty-shaded:1.72.0")
    implementation("io.grpc:grpc-protobuf:1.72.0")
    implementation("io.grpc:grpc-stub:1.72.0")
    implementation("com.google.protobuf:protobuf-java:4.30.2")
    implementation("com.google.protobuf:protobuf-java-util:4.30.2")
    //implementation("jakarta.annotation:jakarta.annotation-api:3.0.0")
    implementation("javax.annotation:javax.annotation-api:1.3.2")

    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.yaml:snakeyaml")

    testImplementation("io.micronaut:micronaut-http-client")
}


application {
    mainClass = "datasahi.flow.Application"
}
java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}


graalvmNative.toolchainDetection = false

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("datasahi.flow.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
        replaceLogbackXml = true
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:4.30.2"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.72.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/java'
            srcDirs 'build/generated/source/proto/main/grpc'
        }
    }
}

tasks.named("dockerfileNative") {
    jdkVersion = "17"
}

tasks.register('bundleZip', Zip) {
    dependsOn shadowJar
    from(layout.buildDirectory.dir("libs")) {
        include '*-all.jar'
    }
    from('src/main/tools') {
        include '*.*'
    }
    archiveFileName = "datasahi-flow-bundle-${version}.zip"
    destinationDirectory = layout.buildDirectory.dir("distributions")
}

// Create tar.gz file
tasks.register('bundleTar', Tar) {
    dependsOn shadowJar

    from(layout.buildDirectory.dir("libs")) {
        include '*-all.jar'
    }
    from('src/main/tools') {
        include '*.*'
    }
    archiveFileName = "datasahi-flow-bundle-${version}.tar.gz"
    destinationDirectory = layout.buildDirectory.dir("distributions")
    compression = Compression.GZIP
}

tasks.register('bundleDistribution') {
    group = 'distribution'
    description = 'Creates distribution bundles containing build/libs jar and src/tools folder contents'
    dependsOn bundleZip
    dependsOn bundleTar
}